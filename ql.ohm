ql {
	Expression = ExpressionComma
	ExpressionComma =
		identifier "=" ExpressionQuery "," ExpressionComma	-- comma
		| ExpressionQuery
	ExpressionQuery =
		ExpressionQuery "where" ExpressionConditional	-- filter
		| ExpressionQuery "map" ExpressionConditional	-- map
		| ExpressionQuery "limit" ExpressionConditional 	-- limit
		| ExpressionQuery "order" ExpressionConditional ("asc"|"desc")?	-- order
		| ExpressionQuery "group" ExpressionConditional	-- group
		| "distinct" ExpressionConditional	-- distinct
		| ExpressionConditional
	ExpressionConditional = ExpressionConditional "?" ExpressionConditional ":" ExpressionConditional	-- conditional
		| ExpressionOr
	ExpressionOr =
		ExpressionOr "|" ExpressionAnd	-- or
		| ExpressionAnd
	ExpressionAnd =
		ExpressionAnd "&" ExpressionNot	-- and
		| ExpressionNot
	ExpressionNot =
		"!" ExpressionNot	-- not
		| ExpressionRelation
	ExpressionRelation =
		ExpressionRelation ("<="|"="|">="|"<"|"!="|">"|"in") ExpressionAdd	-- relation
		| ExpressionAdd
	ExpressionAdd =
		ExpressionAdd ("+"|"-") ExpressionMultiply	-- add
		| ExpressionMultiply
	ExpressionMultiply =
		ExpressionMultiply ("*"|"/") ExpressionAddUnary	-- multiply
		| ExpressionAddUnary
	ExpressionAddUnary =
		("+"|"-") ExpressionAddUnary	-- add
		| ExpressionCount
	ExpressionCount =
		ExpressionCount "#"	-- count
		| ExpressionCall
	ExpressionCall =
		ExpressionMember ExpressionCall	-- call
		| ExpressionMember
	ExpressionMember =
		ExpressionMember "." identifier	-- property
		| ExpressionMember "#" ExpressionAtom	-- index
		| ExpressionAtom
	ExpressionObject = "{" ListOf<ExpressionObjectProperty, ","> "}"
	ExpressionObjectProperty = identifier ":" Expression
	ExpressionArray = "[" ListOf<Expression, ","> "]"
	ExpressionAtom =
		"(" Expression ")"	-- parentheses
		| ExpressionObject
		| ExpressionArray
		| ExpressionThis
		| ExpressionName
		| number
		| string
	ExpressionThis = "this" identifier
	ExpressionName = "::"? identifier
	identifier = ~(("where"|"map"|"limit"|"order"|"asc"|"desc"|"group"|"distinct"|"in") ~(alnum|"_")) &(letter|"_") (alnum|"_")+
	number = digit+ ("." digit+)?
	string = "\"" char* "\""
	char = ~("\""|"\\") any	-- literal
		| "\\" ("\"" | "\\" | "b" | "f" | "n" | "r" | "t" | "v")	-- escaped
	Declarations = Declaration*
	Declaration = identifier "{" DeclarationStatement* "}"
	DeclarationStatement = DeclarationProperty | DeclarationId
	DeclarationProperty = DeclarationPropertyType | DeclarationPropertyValue
	DeclarationPropertyType = identifier ":" identifier ";"
	DeclarationPropertyValue = identifier "=" Expression ";"
	DeclarationId = "id" identifier ";"
}